# 이벤트

이벤트는 온체인 이벤트에 대해 오프체인 리스너에게 알리는 방법입니다. 이벤트는 저장되지 않아
온체인에서 접근할 수 없는 트랜잭션에 대한 추가 정보를 발행하는 데 사용됩니다.
이벤트는 [Sui 프레임워크](./sui-framework)에 위치한 `sui::event` 모듈에 의해 발행됩니다.

> [copy](./../move-basics/copy-ability)와 [drop](./../move-basics/drop-ability) 능력을 가진
> 모든 사용자 정의 타입은 이벤트로 발행될 수 있습니다. Sui Verifier는
> 타입이 모듈 내부에 있어야 한다고 요구합니다.

```move
module sui::event;

/// 사용자 정의 Move 이벤트를 발행하여 데이터를 오프체인으로 전송합니다.
///
/// 특정 애플리케이션에 가장 적합한 방식으로 사용자 정의 인덱스를 생성하고
/// 온체인 활동을 추적하는 데 사용됩니다.
///
/// 타입 `T`는 이벤트를 인덱싱하는 주요 방법이며, 팬텀 매개변수를
/// 포함할 수 있습니다. 예: `emit(MyEvent<phantom T>)`.
public native fun emit<T: copy + drop>(event: T);
```

## 이벤트 발행하기

이벤트는 `sui::event` 모듈의 `emit` 함수를 사용하여 발행됩니다. 이 함수는 단일 인자,
즉 발행할 이벤트를 받습니다. 이벤트 데이터는 값으로 전달됩니다.

```move
module book::events;

use sui::coin::Coin;
use sui::sui::SUI;
use sui::event;

/// 구매할 수 있는 아이템입니다.
public struct Item has key { id: UID }

/// 아이템이 구매될 때 발행되는 이벤트입니다. 아이템의 ID와
/// 구매 가격을 포함합니다.
public struct ItemPurchased has copy, drop {
    item: ID,
    price: u64
}

/// 아이템 구매를 수행하는 마켓플레이스 함수입니다.
public fun purchase(coin: Coin<SUI>, ctx: &mut TxContext) {
    let item = Item { id: object::new(ctx) };

    // `ItemPurchased`의 인스턴스를 생성하고 `event::emit`에 전달합니다.
    event::emit(ItemPurchased {
        item: object::id(&item),
        price: coin.value()
    });

    // 예제를 간단하게 유지하기 위해 나머지 구현을 생략합니다.
    abort
}
```

Sui Verifier는 `emit` 함수에 전달되는 타입이 _모듈 내부에_ 있어야 한다고 요구합니다. 따라서
다른 모듈의 타입을 발행하면 컴파일 오류가 발생합니다. 기본 타입은 _copy_와 _drop_ 요구사항을
충족하지만 이벤트로 발행할 수 없습니다.

## 이벤트 구조

이벤트는 트랜잭션 결과의 일부이며 _transaction effects_에 저장됩니다. 따라서
이벤트는 기본적으로 트랜잭션을 보낸 주소인 `sender` 필드를 가지고 있습니다. 그래서 이벤트에
"sender" 필드를 추가할 필요가 없습니다. 마찬가지로 이벤트 메타데이터에는 타임스탬프가 포함되어 있습니다.
그러나 타임스탬프는 노드에 상대적이며 노드마다 약간 다를 수 있다는 점에 유의해야 합니다.

{/* ## Reliability */}
