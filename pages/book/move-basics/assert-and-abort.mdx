# 실행 중단하기

{/* Consider "aborting execution" */}

{/*

Chapter: Basic Syntax
Goal: Introduce abort keyword and `assert!` macro.
Notes:
    - previous chapter mentions constants
    - error constants standard ECamelCase
    - `assert!` macro
    - asserts should go before the main logic
    - Move has no catch mechanism
    - abort codes are local to the module
    - there are no error messages emitted
    - error codes should handle all possible scenarios in this module

Links:
    - constants (previous section)
 */}

트랜잭션은 성공하거나 실패할 수 있습니다. 성공적인 실행은 객체와 온체인 데이터에 대한 모든 변경 사항을 적용하고, 트랜잭션은 블록체인에 커밋됩니다. 반대로 트랜잭션이 중단되면 변경 사항이 적용되지 않습니다. `abort` 키워드를 사용하여 트랜잭션을 중단하고 수행된 모든 변경 사항을 되돌릴 수 있습니다.

> Move에는 catch 메커니즘이 없다는 점을 유의해야 합니다. 트랜잭션이 중단되면 지금까지 수행된 변경 사항이 되돌려지고, 트랜잭션은 실패로 간주됩니다.

## Abort

`abort` 키워드는 트랜잭션 실행을 중단하는 데 사용됩니다. 중단 코드와 함께 사용되며, 이 코드는 트랜잭션 호출자에게 반환됩니다. 중단 코드는 `u64` 타입의 [정수](./primitive-types)입니다.

```move
let user_has_access = true;

// `user_has_access`가 false이면 미리 정의된 상수로 중단
if (user_has_access) {
    abort 1
};
```

위 코드는 당연히 중단 코드 `1`로 중단됩니다.

## assert!

`assert!` 매크로는 조건을 검증하는 데 사용할 수 있는 내장 매크로입니다. 조건이 거짓이면 트랜잭션은 주어진 중단 코드로 중단됩니다. `assert!` 매크로는 조건이 충족되지 않을 때 트랜잭션을 중단하는 편리한 방법입니다. 이 매크로는 `if` 표현식 + `abort`로 작성될 코드를 단축시킵니다. `code` 인수는 선택 사항이지만 `u64` 값이거나 `#[error]`여야 합니다(자세한 정보는 아래 참조).

```move
// `user_has_access`가 `false`이면 중단 코드 0으로 중단
assert!(user_has_access, 0);

// 다음과 같이 확장됩니다:
if (!user_has_access) {
    abort 0
};
```

## 오류 상수

오류 코드를 더 설명적으로 만들기 위해 [오류 상수](./constants)를 정의하는 것이 좋은 관행입니다. 오류 상수는 `const` 선언으로 정의되며 일반적으로 `E`로 시작하고 카멜 케이스 이름이 뒤따릅니다. 오류 상수는 다른 상수와 유사하며 특별한 처리가 없습니다. 하지만 코드 가독성을 향상시키고 중단 시나리오를 이해하기 쉽게 만들기 위해 일반적으로 사용됩니다.

```move
/// 사용자에게 접근 권한이 없을 때의 오류 코드.
const ENoAccess: u64 = 0;
/// 존재하지 않는 필드에 접근하려 할 때.
const ENoField: u64 = 1;

/// 레코드를 업데이트합니다.
public fun update_record(/* ... , */ user_has_access: bool, field_exists: bool) {
    // assert가 이제 훨씬 더 읽기 쉬워졌습니다
    assert!(user_has_access, ENoAccess);
    assert!(field_exists, ENoField);

    /* ... */
}
```

## 오류 메시지

Move 2024는 `#[error]` 속성으로 표시되는 특별한 타입의 오류 상수를 도입합니다. 이 속성을 사용하면 오류 상수가 `vector<u8>` 타입이 될 수 있으며 오류 메시지를 저장하는 데 사용할 수 있습니다.

```move
#[error]
const ENotAuthorized: vector<u8> = b"The user is not authorized to perform this action";

#[error]
const EValueTooLow: vector<u8> = b"The value is too low, it should be at least 10";

/// 사용자를 대신하여 작업을 수행합니다.
public fun update_value(user: &mut User, value: u64) {
    assert!(user.is_authorized, ENotAuthorized);
    assert!(value >= 10, EValueTooLow);

    user.value = value;
}
```

## 추가 자료

- Move Reference의 [Abort and Assert](./../../reference/abort-and-assert)
- Move에서 오류 처리의 모범 사례를 배우려면 [Better Error Handling](./../guides/better-error-handling) 가이드를 읽어보시기 바랍니다.